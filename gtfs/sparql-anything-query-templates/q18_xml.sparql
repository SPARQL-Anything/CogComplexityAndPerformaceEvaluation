PREFIX fx:  <http://sparql.xyz/facade-x/ns/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT  ?service ?serviceRule ?trip ?route ?longName ?shortName WHERE {

  SERVICE <x-sparql-anything:location=../result/datasets/%format/%size/CALENDAR.%format,%param,slice=%slice,strategy=%strategy> {
    ?calendarContainer  fx:anySlot [ a xyz:service_id ; rdf:_1 ?service_id . ] .
    ?calendarContainer fx:anySlot [ a  xyz:sunday ; rdf:_1 "1" . ] .
    BIND( fx:entity( "http://transport.linkeddata.es/madrid/metro/services/", ?service_id ) AS ?service ) .
    BIND( fx:entity("http://transport.linkeddata.es/madrid/metro/calendar_rules/", ?service_id ) AS ?serviceRule ) .
  }

  SERVICE <x-sparql-anything:location=../result/datasets/%format/%size/TRIPS.%format,%param,slice=%slice,strategy=%strategy> {
    ?tripContainer fx:anySlot [ a xyz:trip_id ; rdf:_1 ?trip_id . ] .
    ?tripContainer fx:anySlot [ a xyz:service_id ; rdf:_1 ?service_id . ] .
    ?tripContainer  fx:anySlot [ a xyz:route_id ; rdf:_1 ?route_id . ] .
    BIND ( fx:entity( "http://transport.linkeddata.es/madrid/metro/routes/", ?route_id ) AS ?route ) .
    BIND ( fx:entity( "http://transport.linkeddata.es/madrid/metro/trips/", ?trip_id ) AS ?trip ) .
  }

  SERVICE <x-sparql-anything:location=../result/datasets/%format/%size/ROUTES.%format,%param,slice=%slice,strategy=%strategy> {
    ?routeContainer fx:anySlot [ a xyz:route_id ; rdf:_1 ?route_id . ] .
    { ?routeContainer  fx:anySlot [ a xyz:route_short_name ; rdf:_1 ?shortName ] } UNION { ?routeContainer  fx:anySlot [ a xyz:route_long_name ; rdf:_1 ?longName ]  }
  }

}
